% Annotated example from Simula Standard sect. 7.3.1

begin comment S1;
      ref(C1) X1;
      class C1;
      begin procedure P1; detach;
            outtext("Before Detach C1 - see fig. 7.1"); outimage;
            P1;
      end C1;
      ref(C2) X2;
      class C2;
      begin procedure P2;
            begin
               outtext("Before Detach C2 - see fig. 7.6"); outimage;
               detach;
               outtext("After  Detach C2 - see fig. 7.6 again"); outimage;
            end P2;
            begin comment system S2;
                  ref(C3) X3;
                  class C3;
                  begin outtext("Before Detach C3 - see fig. 7.3"); outimage;
                        detach;
                        outtext("After  Resume X3 - see fig. 7.5"); outimage;
                        P2;
                  end C3;
                  X3:- new C3;
                  outtext("After  Detach C3 - see fig. 7.4"); outimage;
                  resume(X3)
            end S2;
      end C2;
      
      
      outtext("--------------------------------------------------------"); outimage;
      outtext("Begin Annotated example from Simula Standard sect. 7.3.1"); outimage;
      outtext("--------------------------------------------------------"); outimage;
      outtext("Expected Output:"); outimage;
      outtext("--------------------------------------------------------"); outimage;
      outtext("Before Detach C1 - see fig. 7.1"); outimage;
      outtext("After  Detach C1 - see fig. 7.2"); outimage;
      outtext("Before Detach C3 - see fig. 7.3"); outimage;
      outtext("After  Detach C3 - see fig. 7.4"); outimage;
      outtext("After  Resume X3 - see fig. 7.5"); outimage;
      outtext("Before Detach C2 - see fig. 7.6"); outimage;
      outtext("After  Detach C2 - see fig. 7.7"); outimage;
      outtext("After  Detach C2 - see fig. 7.6 again"); outimage;
      outtext("Final END of Example"); outimage;
      outtext("--------------------------------------------------------"); outimage;
      outtext("Actual Output:"); outimage;
      outtext("--------------------------------------------------------"); outimage;
      
      X1:- new C1;
      outtext("After  Detach C1 - see fig. 7.2"); outimage;
      X2:- new C2;
      outtext("After  Detach C2 - see fig. 7.7"); outimage;
      call(X2);
      outtext("Final END of Example"); outimage;
      outtext("--------------------------------------------------------"); outimage;
      
end S1;